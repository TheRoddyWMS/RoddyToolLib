/*
 * Copyright (c) 2017 German Cancer Research Center (DKFZ).
 *
 * Distributed under the MIT License (license terms are at https://www.github.com/theroddywms/Roddy/LICENSE.txt).
 */

import org.cyclonedx.gradle.CycloneDxTask

plugins {
    id 'org.ajoberstar.grgit' version '1.7.2'
    id 'org.cyclonedx.bom' version '1.8.2'
}

apply plugin: 'groovy'
apply plugin: 'maven'

group = "com.github.theroddywms"


/*
 * Gets the version name from the current Git tag. If the current commit is not tagged,
 * this returned string will indicate that. Also if the repository is dirty.
 */
def getVersionName() {
    def dirtySuffix = grgit.status().isClean() ? '' : '-dirty'
    return grgit.describe() + dirtySuffix
}

rootProject.version = getVersionName()

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.4.9'
    testCompile 'junit:junit:4.12'
    testCompile 'org.spockframework:spock-core:1.1-groovy-2.4'
    compile group: 'commons-codec', name: 'commons-codec', version: '1.9'
    compile group: 'org.apache.commons', name: 'commons-io', version: '1.3.2'
}

// Fix Java Version
ext.langLevel = 1.8

task writePom {
    doLast {
        pom {
            project {
                version rootProject.version
                inceptionYear '2017'
                licenses {
                    license {
                        name 'MIT License'
                        url 'https://raw.githubusercontent.com/eilslabs/RoddyToolLib/master/LICENSE'
                        distribution 'repo'
                    }
                }
            }
        }.writeTo("$buildDir/pom.xml")
    }
}

jar {
    version = getVersionName()
    manifest {
        attributes("Implementation-Vendor": "German Cancer Research Center (DKFZ)")
        attributes("Implementation-Title": baseName)
        attributes 'Implementation-Version': rootProject.version
    }

    dependsOn writePom
    into("META-INF/maven/$project.group/$project.name") {
        from "$buildDir/pom.xml"
    }
}

test {
    workingDir = System.getProperty("user.dir")
    ignoreFailures = project.hasProperty("ignoreFailedTests")

    testLogging {
        debug {
            events "started", "skipped", "failed"
            exceptionFormat "full"
        }
        events "failed"
        exceptionFormat "full"
        setShowStandardStreams true
    }
}

task listConfigurations {
    group = "reporting"
    description = "List all configurations, as needed for discerning development and production configurations by the SBOM tasks."
    doLast {
        configurations.each { Configuration c ->
            println c.name
        }
    }
}

task allBoms() {
    // a grouping task to execute the CycloneDxTask for each configuration
    group = "reporting"
}

rootProject.configurations.each { configuration ->
    String name = configuration.name
    task "${name}Bom"(type: CycloneDxTask) {
        group = "reporting"
        description = "Creates a CycloneDX SBOM for the configuration ${name}."

        destination = file("${project.buildDir}/reports/cyclonedx")
        outputName = name
        includeConfigs = [name]
        schemaVersion = "1.5"
        outputFormat="json"
        includeLicenseText = false
        projectType = "application"
    }
    allBoms.dependsOn tasks.getByName("${name}Bom")
}
